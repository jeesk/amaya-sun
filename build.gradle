plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    compileJava.options.encoding('UTF-8')
    withJavadocJar()
    withSourcesJar()
}

group = 'io.github.amayaframework'
archivesBaseName = 'amaya-sun'
version = System.getenv('RELEASE_VERSION') ?: '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    api group: 'org.atteo.classindex', name: 'classindex', version: '3.11'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'
    implementation group: 'io.github.amayaframework', name: 'http-server', version: '1.1.1'
    api group: 'io.github.amayaframework', name: 'amaya-core', version: '1.2.2'
}

jar {
    from sourceSets.main.output
    from sourceSets.main.allJava
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // Specify artifacts
            groupId = group
            artifactId = archivesBaseName
            version = version
            from components.java
            // Configure pom
            pom {
                name.set(archivesBaseName)
                description.set('Amaya is a fairly lightweight web framework for Java, which guarantees speed, ' +
                        'ease of creating plugins/addons, flexibility and ease of use. Sun server implementation.')
                url.set('https://github.com/AmayaFramework/amaya-sun')
                organization {
                    name.set('io.github.amayaframework')
                    url.set('https://github.com/AmayaFramework')
                }
                issueManagement {
                    system.set('GitHub')
                    url.set('https://github.com/AmayaFramework/amaya-sun/issues')
                }
                licenses {
                    license {
                        name.set('GNU General Public License v3.0')
                        url.set('https://github.com/AmayaFramework/amaya-sun/blob/main/LICENSE')
                    }
                }
                scm {
                    url.set('https://github.com/AmayaFramework/amaya-sun')
                    connection.set('scm:https://github.com/AmayaFramework/amaya-sun.git')
                    developerConnection.set('scm:https://github.com/AmayaFramework/amaya-sun.git')
                }
                developers {
                    developer {
                        id.set('RomanQed')
                        name.set('Roman Bakaldin')
                        email.set('gbakaldin@gmail.com')
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username System.getenv('SONATYPE_USERNAME')
                password System.getenv('SONATYPE_PASSWORD')
            }
        }
    }
}